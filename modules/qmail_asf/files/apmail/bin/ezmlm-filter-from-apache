#!/usr/bin/perl -n
#
# if a message arrives without a From: header with an email address of
# apache.org, bounce it back.
#
# For this to be effective, it needs to be added to the "editor" file
# in an ezmlm list dir, probably after any spam filtering.

use POSIX '_exit'; # bypasses END block, doesn't flush io buffers
use File::Basename;
use lib dirname(dirname $0) . "/lib";
use pull_header;

# exit with a fatal error if the From header doesn't have what it
# should.

/^from:/i and pull_header {
    /\@\S*\bapache\.org\b/i and _exit 0;
    syswrite STDOUT, "Must be sent from an \@apache.org address.\n";
    _exit 100;
};

last if /^$/;   # stop at end of headers

END { exit 99 } # drop message if we've not seen a From header
